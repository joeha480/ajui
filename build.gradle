// For bnd
buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.3.0'
	}
}

plugins {
	id 'java'
	id 'maven'
	id 'signing'
}

apply plugin: 'biz.aQute.bnd.builder'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

/*
The following environment variables needed for artifact publishing:

	TRAVIS_BUILD_ID
	SONATYPE_PASSWORD
	SONATYPE_USER
*/

group = "com.googlecode.ajui"

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'src'
		}
	}
	test {
		java {
			srcDir 'test'
		}
		resources {
			srcDir 'test'
		}
	}
}

def repoRevision = System.getenv("REPO_REVISION")!=null?System.getenv("REPO_REVISION"):repositoryRevision

javadoc {
	options.encoding = 'UTF-8'
	options.links('http://docs.oracle.com/javase/8/docs/api/')
}

repositories {
    mavenCentral()
}

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
	manifest {
		attributes(
			'-sources': 'true',
			'Export-Package':  'com.googlecode.ajui,'+
								'com.googlecode.ajui.event,'+
								'com.googlecode.ajui.comp',
			'Service-Component': '*',
			'Bundle-Name': "$moduleName",
			'Bundle-SymbolicName': "$moduleName",
			'Built-By': System.getProperty("user.name"),
			'Built-On': new Date().format('yyyy-MM-dd'),
			'Repository-Revision': "$repoRevision",
			'Repository-URL': "$repositoryURL",
			'Include-Resource': 'LICENSE, NOTICE'
		)
	}
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
	//Only upload if a git hash is supplied. On Travis only upload snapshots.
	enabled = repoRevision.size()==40 && System.getenv("TRAVIS_BUILD_ID")!=null
    repositories {
        mavenDeployer {
            if (isReleaseVersion) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv("SONATYPE_USER"), password: System.getenv("SONATYPE_PASSWORD"))
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv("SONATYPE_USER"), password: System.getenv("SONATYPE_PASSWORD"))
            }
            pom.project {
                name 'ajui'
                packaging 'jar'
                description 'Accessible Java GUI'
                url 'https://github.com/joeha480/ajui/'
                scm {
                    connection 'scm:git:https://github.com/joeha480/ajui.git'
                    developerConnection 'scm:git:https://github.com/joeha480/ajui.git'
                    url "$repositoryURL"
                }
                licenses {
                    license {
                        name 'LGPL'
                        url 'http://www.gnu.org/licenses/lgpl.html'
                    }
                }
                developers {
                    developer {
                        id 'joel'
                        name 'Joel HÃ¥kansson'
                    }
                }
            }
        }
    }
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}